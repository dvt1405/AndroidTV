plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'com.kt.apps.media.xemtv'
    compileSdk extCompileSdkVersion.tv

    defaultConfig {
        applicationId "com.kt.apps.media.xemtv"
        minSdk extMinSdkVersion.tv
        targetSdk extCompileSdkVersion.tv
        versionCode extVersionCode.tv
        versionName extVersionName.tv
        setProperty("archivesBaseName", "iMedia.$versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }

    }

    signingConfigs {
        release {
            storeFile file("$rootDir/keys/Untitled")
            storePassword props.getProperty("pass")
            keyAlias props.getProperty("alias.tv")
            keyPassword props.getProperty("pass")
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ".Dev"
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    buildFeatures {
        buildConfig = true
    }
    flavorDimensions "version"
    productFlavors {

        playStore {
            dimension "version"
            buildConfigField "boolean", "isBeta", "false"
        }

        modBeta {
            dimension "version"
            versionNameSuffix ".Beta"
            buildConfigField "boolean", "isBeta", "true"
        }

        apkPure {
            dimension "version"
            applicationId "com.kt.apps.media.xemtv.beta"
            versionName "${extVersionName.tv}.B"
            buildConfigField "boolean", "isBeta", "true"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
    sourceSets {
        androidTest {
            java.srcDirs += ["${project(':core').projectDir}/src/androidTest/java"]
            res.srcDirs += ["${project(':core').projectDir}/src/androidTest/res"]
            assets {
                srcDirs += ["${project(':core').projectDir}/src/androidTest/assets"]
            }
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation 'androidx.leanback:leanback:1.1.0-beta01'
    implementation("androidx.multidex:multidex:2.0.1")
    implementation appDependencies.values()
    implementation "androidx.leanback:leanback-paging:1.1.0-alpha07"
    implementation "androidx.leanback:leanback-tab:1.1.0-beta01"
    implementation workManager.values()
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation testDependencies.values()
    testImplementation project(path: ':core')
    androidTestImplementation project(path: ':core')
    androidTestImplementation project(path: ':core')
    kaptTest kotlinTestDependencies.values()
    kaptAndroidTest kotlinAndroidTestDependencies.values()
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "com.google.firebase:testlab-instr-lib:0.2"
    implementation daggerDependencies.values()
    implementation retrofitDependencies.values()
    implementation okhttpDependencies.values()
    kapt complier.values()
    implementation platform("com.google.firebase:firebase-bom:$firebaseBomVersion")
    implementation firebaseDependencies.values()
    implementation externalDependencies.values()
    implementation authen.values()
    implementation roomDatabase.values()
    implementation exoPlayerDependencies.values()
    implementation project(":skeleton")
    implementation project(":dialoglib")
    implementation project(":core")
    implementation project(":tv")
    implementation project(":football")
    implementation project(":autoupdate")
    implementation project(':resources')
    implementation project(":voiceselector")
    implementation otherLibs.values()
    implementation rxJavaDependencies.values()

}